plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id 'xyz.jpenilla.run-paper' version '+'
    id 'checkstyle'
    id 'jacoco'
    id 'idea'
    id('io.ebean') version '+'
    id("io.papermc.paperweight.userdev") version "1.7.2"
}

group = 'com.carinaschoppe'
version = '1.0'

repositories {

    maven {
        url = 'https://repo.extendedclip.com/content/repositories/placeholderapi/'
    }


    mavenCentral()
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
    maven { url "https://repo.dmulloy2.net/repository/public/" }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }

    flatDir {
        dirs 'dependentPlugins' // Verzeichnispfad anpassen
    }
}

dependencies {

    //placehodler API
    //compileOnly 'com.comphenix.protocol:ProtocolLib:5.1.0'
    compileOnly name: 'individualsigns'
    //ORM Ebean
    implementation 'io.ebean:ebean-ddl-generator:+'
    implementation 'io.ebean:ebean:15.5.1'
    implementation 'io.ebean:ebean-querybean:15.5.1'
    annotationProcessor 'io.ebean:querybean-generator:15.5.1'
    testImplementation 'io.ebean:ebean-test:15.5.1'

    //protocollib
    //compileOnly 'com.comphenix.protocol:ProtocolLib:+'

    //junit
    testImplementation 'org.junit.jupiter:junit-jupiter-api:+'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:+'

    //gson
    implementation("com.google.code.gson:gson:+")

    //lombok
    compileOnly 'org.projectlombok:lombok:+'
    annotationProcessor 'org.projectlombok:lombok:+'
    testCompileOnly 'org.projectlombok:lombok:+'
    testAnnotationProcessor 'org.projectlombok:lombok:+'

    //mockito
    testImplementation 'org.mockito:mockito-core:+'

    //paper
    paperweight.paperDevBundle("1.21.1-R0.1-SNAPSHOT")


    //sqlite
    implementation 'org.xerial:sqlite-jdbc:+'
}

def targetJavaVersion = 21
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

tasks {
    shadowJar {
        archiveClassifier.set('')  // Keine zusätzliche Bezeichnung, damit es die Haupt-JAR ist

        // Sicherstellen, dass META-INF/services Dateien zusammengeführt werden
        mergeServiceFiles {
            include 'META-INF/services/*'
        }
    }

    build {
        dependsOn jacocoTestReport
        dependsOn shadowJar
    }
}

jacocoTestReport {
    reports {
        xml.required = true
    }
}

checkstyle {
    toolVersion = '10.18.0' // Version von Checkstyle, die du verwenden möchtest
    configFile = file('config/checkstyle/checkstyle.xml') // Dein eigenes Regelset
}

checkstyleMain {
    source = 'src/main/java'
}
checkstyleTest {
    source = 'src/test/java'
}

test {
    useJUnitPlatform()
}
runServer {
    minecraftVersion("1.21.1")
}

defaultTasks 'build'

